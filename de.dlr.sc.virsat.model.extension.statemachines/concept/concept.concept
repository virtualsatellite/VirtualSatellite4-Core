/*******************************************************************************
 * Copyright (c) 2008-2019 German Aerospace Center (DLR), Simulation and Software Technology, Germany.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
Concept de.dlr.sc.virsat.model.extension.statemachines displayname "State Machines" description "Concept for modeling State Machines"  version 1.6 beta {
	Import de.dlr.sc.virsat.model.extension.ps.*; 
	
	Category State extends StateMachine description "State of the component at a given time"{
		StringProperty detail description "Optional additional detail information about the state";
		FloatProperty maxTime description "The maximum time spent in the state" default 0.0 quantityKind "Time" unit "Second";
	}
	
	Category Transition description "Transitions among states" { 
		Reference stateFrom of Type State description "The Transition starts in this state";
		Reference stateTo of Type State description "The Transition ends in this state";
		Reference trigger of Type TransitionTriggerEvent description "The event that triggers the Transition";
	}
	
	Category TransitionTriggerEvent description "Some kind of event or action that triggers a transition" {
	/*
	** types can be used to model the source of the transition trigger
	** Asynchronous means that this transition trigger can only be executed sequentially
	** Synchronous means that this transition trigger can be executed in parallel with other triggers as it can be executed automatically or be the result of a reconfiguration
	*/ 
		EnumProperty type values [ 
			asynchronous=1,
			synchronous=2
		] default asynchronous;
		
		StringProperty detail description "Optional additional detail information about this trigger";
		
		EnumProperty sending values [ 
			send=1,
			receive=2
		] default send;
		
	}
	
	Category AConstraint description "General constraint in statemachines "{
		IsAbstract;
		Reference stateConstraining of Type State description "The state that constraints the influenced state";
		Reference stateInfluenced of Type State description "The state that is influenced by this constraint";
	}
	
	Category AllowsConstraint  extends AConstraint description "A state explicitly allows another state" {
		
	}
	
	Category ForbidsConstraint  extends AConstraint description "A state forbids another state" { 
		
	}
	
	Category StateMachine description "A state machine with states and transitions" {
		Applicable For [ElementDefinition, ElementConfiguration, ElementOccurence];
		
		EnumProperty type values [ 
			SoftwareStateMachine=1,
			HardwareStateMachine=2
		] default SoftwareStateMachine;
		
	
		Reference initialState of Type State description "The initial state of this state machine" ;
		Type states[] of Category State description "A collection of states of this state machine";
		Type transitions[] of Category Transition description "A collections of transitions of this state machine";
		Type constraints[] of Category AConstraint description "A collection of constraints of this state machine";
		Type triggerEvents[] of Category TransitionTriggerEvent description "List of explicit events that trigger a transition";
	}
}