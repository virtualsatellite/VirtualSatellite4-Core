/*******************************************************************************
 * Copyright (c) 2008-2019 German Aerospace Center (DLR), Simulation and Software Technology, Germany.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
Concept de.dlr.sc.virsat.model.extension.ps displayname "Product Structure" description "Product Structure Concept"  version 1.3 {
	
	// --- Product Tree ---
	StructuralElement ProductTree description "Product tree to describe specifications of used components" { 
		IsRootStructuralElement;
	}
	 
	StructuralElement ProductTreeDomain description "A domain representing a folder in which component specifications can be organized" {  
		Applicable For [ProductTree, ProductTreeDomain];
	}
	
	StructuralElement ElementDefinition description "A component representing the specification of it" { 
		Applicable For [ProductTreeDomain];
	} 
	 
	// --- Configuration Tree ---	
	StructuralElement ConfigurationTree description "Configuration tree representing how components are put together" { 
		Inherits From [ElementDefinition];
		IsRootStructuralElement;  
	} 
	 
	StructuralElement ElementConfiguration description "A components representing the specification of how components are connected" {    
		Inherits From [ElementDefinition];
		Applicable For [ConfigurationTree, ElementConfiguration];
	}
	
	// --- Assembly Tree ---	
	StructuralElement AssemblyTree description "Assembly tree to represent how components are actually integrated" {
		Inherits From [ConfigurationTree];
		IsRootStructuralElement;
	}
	
	StructuralElement ElementOccurence description "A component representing the integrated state within an assembly tree"{
		Inherits From [ElementConfiguration, ElementRealization];
		Applicable For [AssemblyTree, ElementOccurence];
	} 

	// --- Storage ---	
	StructuralElement ProductStorage description "Product tree to describe a storage of real components" { 
		IsRootStructuralElement;
	}
	
	StructuralElement ProductStorageDomain description "A domain representing a folder in which real component can be organized within the storage" {  
		Applicable For [ProductStorage, ProductStorageDomain];
	}
	
	StructuralElement ElementRealization description "A component representing the real product" {    
		Inherits From [ElementDefinition];
		Applicable For [ProductStorageDomain];
	}

	// --- Documents ---	
	Category Document description "Category to describe documents such as specifications" {
		Applicable For [ElementDefinition, ElementConfiguration, ElementOccurence, ElementRealization];
		StringProperty documentName description "Name of the document"; 
		StringProperty note description "Some not of the document";
		StringProperty url description "A reference URL to the document";
		Resource file description "The actual file of the document";
	}
}

